	MODULE trfil

;TR-DOS File System module

	include "radxas.a80"

CAT_ADR	EQU	0B000H

RD_CAT:	CALL	DRVSEL
	LD	HL,CAT_ADR
	LD	DE,0
	LD	B,9
	CALL	RDGR_E
	LD	IX,CAT_ADR+880H
	LD	A,(IX+64H)
	LD	(MAXFILES),A
	LD	A,(IX+67H)
	CP	10H
	RET	Z
	LD	A,5
	CALL	DSK_ERR
	JR	RD_CAT

WR_CAT:	LD	HL,CAT_ADR
	LD	DE,0
	LD	B,9
	JP	WRGR_E

;<DE=FileMask Ptr (?=Any Sym)
;>CAT_ADR+900 - Table
SCAN_D:	LD	HL,CAT_ADR
	LD	IX,CAT_ADR+901H
	LD	A,(MAXFILES)
	LD	B,A
	LD	C,0
SCND1:	PUSH	HL
	PUSH	DE
	PUSH	BC
	CALL	CMP_DIR
	POP	BC
	POP	DE
	POP	HL
	JR	NZ,SCND0
	LD	A,(MAXFILES)
	SUB	B
	LD	(IX),A
	INC	IX
	INC	C
SCND0:	CALL	ADHL_16
	DJNZ	SCND1
	LD	A,C
	LD	(CAT_ADR+900H),A
P_RET:	RET

CMP_DIR:
	LD	BC,11
CMP_MEM:
	LD	A,(DE)
	INC	DE
	CP	'?'
	JR	NZ,CMPD1
	LD	A,(HL)
CMPD1:	CPI
	RET	NZ
	JP	PE,CMP_MEM
	RET

ADHL_16:
	LD	A,L
	ADD	A,16
	LD	L,A
	ADC	A,H
	SUB	L
	LD	H,A
	RET

;Load File
;<HL=loading addr
;<DE=fcb adr
LD_FIL:	PUSH	HL
	EX	DE,HL
	LD	BC,13
	ADD	HL,BC
	LD	B,(HL)
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	POP	HL
	CALL	RDGR_E
	RET

;Save file from pages directly
;<DE=fcb adr
SV_FIL:	LD	HL,CAT_ADR
	LD	C,0
SV_FI0:	LD	A,(HL)
	OR	A
	JR	Z,SV_FND
	DEC	A
	JR	Z,SV_DEL
	PUSH	HL
	PUSH	DE
	PUSH	BC
	LD	BC,9
	CALL	CMP_MEM
	POP	BC
	POP	DE
	POP	HL
	LD	A,1
	JP	Z,FAT_ERR
SV_DEL:	CALL	ADHL_16
	INC	C
	JP	P,SV_FI0
	LD	A,2
	JP	FAT_ERR
SV_FND:	EX	DE,HL
	PUSH	HL
	POP	IY
	LD	IX,CAT_ADR+880H
	INC	(IX+64H)	;NFiles
	LD	L,(IX+61H)	;Fre Sec
	LD	(IY+0EH),L
	LD	H,(IX+62H)	;Fre Trk
	LD	(IY+0FH),H
	LD	C,(IY+0DH)
	LD	B,0
	ADD	HL,BC
	LD	A,L
	RRCA
	RRCA
	RRCA
	RRCA
	AND	0FH
	ADD	A,H
	LD	H,A
	LD	A,L
	AND	0FH
	LD	L,A
	LD	(IX+61H),L
	LD	(IX+62H),H
	LD	L,(IX+65H)	;Free
	LD	H,(IX+66H)	;sec qty
	OR	A
	SBC	HL,BC
	LD	A,3
	JP	C,FAT_ERR
	LD	(IX+65H),L
	LD	(IX+66H),H
	PUSH	IY
	POP	HL
	LD	C,16
	LDIR
	XOR	A
	LD	(DE),A
;Saving sectors
	DB	0DDH
	LD	L,53H
	CALL	SVF_C
	LD	A,50H
	OUT	(0FDH),A
	JP	WR_CAT

SVF_C:	LD	E,(IY+0EH)
	LD	D,(IY+0FH)
SVSCCY:	DB	0DDH
	LD	A,L
	OUT	(0FDH),A
	LD	HL,0C000H
	LD	A,(IY+0DH)
	LD	B,40H
	SUB	B
	PUSH	AF
	JR	NC,SV_FI1
	LD	B,(IY+0DH)
SV_FI1:	LD	(IY+0DH),A
	CALL	WRGR_E
	POP	AF
	RET	Z
	RET	C
	DB	0DDH
	LD	A,L
	INC	A
	CP	55H
	JR	NZ,SVFC0
	INC	A
SVFC0:	DB	0DDH
	LD	L,A
	JR	SVSCCY

RDGR_E:	CALL	radxas.READGR
RDGRE0:	LD	A,(radxas.ERR_CO)
	OR	A
	RET	Z
	CALL	DSK_ERR
	CALL	radxas.RETRY
	JR	RDGRE0
WRGR_E:	CALL	radxas.WRITGR
	JR	RDGRE0

FAT_ERR:
	PUSH	AF
	LD	HL,ERR_WND
	CALL	wind.PRINTW
	POP	AF
	LD	HL,FAT_MSG
	CALL	wind.SOBN
	CALL	wind.SOUND2
	LD	HL,FAT_PAK
	CALL	wind.PRINTS
	CALL	spkeyb.CONIN
	JP	xasconv.ABORT

DSK_ERR:
	PUSH	AF
	LD	HL,ERR_WND
	CALL	wind.PRINTW
	POP	AF
	DEC	A
	LD	HL,ERR_MSG
	CALL	wind.SOBN
	CALL	wind.SOUND2
	LD	IX,ERR_MENU
	CALL	wind.IMENU
REM0:	CALL	wind.RMENU
	JP	Z,wind.ENDW
	CP	27
	JR	NZ,REM0

DRVSEL:	LD	A,(xasconv.SY_DRV)
	SUB	'A'
	CALL	radxas.DRIVE
	JP	radxas.INIALL

MAXFILES:
	DB 0

ERR_WND:
	DB 8,10
	DB 16,5
	DB 01010111B
	DB 00000001B
	DB 0,0
	DB 0,0

ERR_MSG:
	db 1,'DRIVE NOT READY',0
	db 1,'DISK READ ONLY',0
	db 1,'BREAK KEY PRESSED',0
	db 1,'DISK ERROR',0
	db 1,'WRONG DISK',0

FAT_MSG:
	db 1,'FILE NOT FOUND',0
	db 1,'FILE EXISTS',0
	db 1,'DIRECTORY FULL',0
	db 1,'NO DISK SPACE',0
	db 1,'INVALID FILE',0
	db 1,'FILE OVERFLOW',0

FAT_PAK:
	db 13,13,23,128,1,'ABORT',23,0,0

ERM_I0:	db 'ABORT',0
ERM_I1:	db 'RETRY',0
ERR_MENU:
	DB 0

	DB 0,2,5,17H
	DW ERM_I0
	DW 0
	DW 1
	DW 0
	DW 0
	DW xasconv.ABORT

	DB 9,2,5,17H
	DW ERM_I1
	DW 0
	DW 1
	DW 0
	DW 0
	DW P_RET

	DB 0FFH

	ENDMODULE
