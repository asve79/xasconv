	module cnvexe

CNVXAS	LD	HL,0
	LD	(OUTP_LEN),HL
	LD	(LINES_PROC),HL
	LD	HL,0C024H
	LD	(INP_PTR),HL
	LD	DE,OUTP_BUF
	LD	A,';'
	LD	(DE),A
	INC	DE
	LD	HL,0C000H
	LD	BC,1DH
	LDIR
	LD	A,0DH
	LD	(DE),A
	INC	DE
	LD	A,0AH
	LD	(DE),A
	INC	DE
	CALL	OUTP_INS

MNCBG:	LD	B,10
MNCYC:	PUSH	BC
	CALL	CNVLINE
	POP	BC
	JR	Z,CXAS_END
	DJNZ	MNCYC
	CALL	OUTP_PRN
	JR	MNCBG

CXAS_END:
	CALL	OUTP_PRN
	LD	HL,MSG_COMP
	CALL	wind.PRINTS
	LD	HL,(OUTP_LEN)
	RET

;컴컴컴컴컴컴컴컴컴컴컴컴컴
CNVLINE:
	LD	HL,(INP_PTR)
	LD	A,(HL)
	OR	A
	JR	Z,CNVL_TXE
	LD	DE,OUTP_BUF
	CALL	STRBG
	LD	(INP_PTR),HL
	CALL	OUTP_INS
	OR	1
	RET
CNVL_TXE:
	LD	HL,(LINES_PROC)
	DEC	HL
	LD	(LINES_PROC),HL
	LD	DE,OUTP_BUF
	LD	A,1AH
	LD	(DE),A
	INC	DE
	CALL	OUTP_INS
	XOR	A
	RET

STRBG:	LD	C,0
	CALL	CKCRLF
	RET	NC
	CP	80H
	JR	C,FMET
	LD	A,9
	LD	(DE),A
	INC	DE
	LD	A,(HL)
	CALL	CNVTOK
	INC	HL
	CALL	CKCRLF
	RET	NC
	JR	CYCO9
FMET:	INC	C
	CALL	CKCRLF
	RET	NC
	CP	80H
	JR	NC,METEND
	INC	HL
	LD	(DE),A
	INC	DE
	JR	FMET
METEND:	LD	A,':'
	LD	(DE),A
	INC	DE
	LD	A,C
	CP	8
	CALL	NC,FTCRLF
	JR	STRBG

CYCO9:	LD	A,9
	LD	C,0
CYCOP:	LD	(DE),A
	INC	DE
CYCO0:	LD	A,(HL)
	CP	80H
	JR	C,FTEX
	CALL	CNVTOK
CYCO1:	INC	HL
	CALL	CKCRLF
	RET	NC
COMOP:	LD	A,','
	BIT	1,C
	JR	Z,CYCOP
	JR	CYCO0

FTEX:	CALL	CKCRLF
	RET	NC
	CP	80H
	JR	NC,COMOP
	CP	'#'
	JR	Z,FNUM
	CP	'('
	JR	Z,FT_LB
	CP	')'
	JR	NZ,FTEX0
	RES	1,C
	LD	(DE),A
	INC	DE
	JR	CYCO1
FT_LB:	SET	1,C
FTEX0:	LD	(DE),A
	INC	DE
	INC	HL
	JR	FTEX

FNUM:	SET	0,C
	INC	HL
FNUMC:	LD	A,(HL)
	CALL	ISDIG
	JR	C,FNUME
	JR	Z,FNUM0
	BIT	0,C
	JR	Z,FNUM0
	PUSH	AF
	LD	A,'0'
	LD	(DE),A
	INC	DE
	POP	AF
FNUM0:	LD	(DE),A
	INC	DE
	INC	HL
	RES	0,C
	JR	FNUMC
FNUME:	LD	A,'H'
	LD	(DE),A
	INC	DE
	JR	FTEX

CNVTOK:	PUSH	HL
	SUB	80H
	LD	HL,TOKTBL
CNVTO1:	OR	A
	JR	Z,TOKFND
CNVTO0:	BIT	7,(HL)
	INC	HL
	JR	Z,CNVTO0
	DEC	A
	JR	CNVTO1
TOKFND:	LD	A,(HL)
	AND	127
	CP	(HL)
	INC	HL
	LD	(DE),A
	INC	DE
	JR	Z,TOKFND
	POP	HL
	RET

ISDIG:	CP	'0'
	RET	C
	CP	'F'+1
	CCF
	RET	C
	CP	'9'+1
	JR	C,IDGZ
	CP	'A'
	RET	C
	OR	A
	RET
IDGZ:	CP	A
	RET

CKCRLF:	CALL	GSCE
	RET	NZ
	CP	';'
	JR	Z,CRCOM
CRLF:	INC	HL
FTCRLF:	LD	A,0DH
	LD	(DE),A
	INC	DE
	LD	A,0AH
	LD	(DE),A
	INC	DE
	XOR	A
	RET

CRCOM:	LD	(DE),A
	INC	DE
	INC	HL
	CALL	GSCE
	JR	Z,CRLF
	JR	CRCOM

GSCE:	LD	A,(HL)
	CP	0DH
	RET	Z
	CP	09H
	RET	Z
	CP	0CH
	RET	Z
	CP	';'
	SCF
	RET

;컴컴컴컴컴컴컴컴컴컴컴컴컴

;Insert buffer into output file
;<DE=buffer end ptr
OUTP_INS:
	EX	DE,HL
	LD	DE,OUTP_BUF
	OR	A
	SBC	HL,DE
	LD	B,H
	LD	C,L
	LD	HL,(OUTP_LEN)
	PUSH	HL
	ADD	HL,BC
	LD	A,H
	INC	A
	LD	A,5	;Output ovfl
	JP	Z,trfil.FAT_ERR
	LD	(OUTP_LEN),HL
	POP	HL
	EX	DE,HL
	CALL	LIR_PAG
	LD	HL,(LINES_PROC)
	INC	HL
	LD	(LINES_PROC),HL
	RET
;Transfer into page
;<DE=abs. buf addr
;<HL=memory adr
;<BC=length
LIR_PAG:
	LD	A,D
	RLCA
	RLCA
	AND	3
	CP	2
	CCF
	ADC	A,53H
	OUT	(0FDH),A
	PUSH	DE
	LD	A,D
	OR	0C0H
	LD	D,A
	LDI
	POP	DE
	INC	DE
	JP	PE,LIR_PAG
	LD	A,50H
	OUT	(0FDH),A
	RET

OUTP_PRN:
	LD	HL,OUTP_MSG
	JP	wind.PRINTS

OUTP_MSG:
	DB 22,17,1
	DB 4
	DW LINES_PROC
	DB 22,17,2
	DB 4
	DW OUTP_LEN
	db 0

MSG_COMP:
	DB 22,0,3
	db 1,'Complete',0

OUTP_BUF:
	DS 44
INP_PTR:
	DW	0
OUTP_LEN:
	DW	0
LINES_PROC:
	DW	0

TOKTBL:	DC 'LDIR'
	DC 'LDDR'
	DC 'LDI'
	DC 'LDD'
	DC 'CPIR'
	DC 'CPDR'
	DC 'CPI'
	DC 'CPD'
	DC 'INIR'
	DC 'INDR'
	DC 'INI'
	DC 'IND'
	DC 'OUTI'
	DC 'OTIR'
	DC 'OUTD'
	DC 'OTDR'
	DC 'RETI'
	DC 'RETN'
	DC 'NEG'
	DC 'RLD'
	DC 'RRD'
	DC 'PUSH'
	DC 'POP'
	DC 'ADD'
	DC 'SUB'
	DC 'ADC'
	DC 'SBC'
	DC 'AND'
	DC 'OR'
	DC 'XOR'
	DC 'CP'
	DC 'INC'
	DC 'DEC'
	DC 'BIT'
	DC 'RES'
	DC 'SET'
	DC 'RLC'
	DC 'RRC'
	DC 'RL'
	DC 'RR'
	DC 'SLA'
	DC 'SRA'
	DC 'SLI'
	DC 'SRL'
	DC 'LD'
	DC 'EX'
	DC 'IN'
	DC 'OUT'
	DC 'IM'
	DC 'RST'
	DC 'DJNZ'
	DC 'JP'
	DC 'JR'
	DC 'CALL'
	DC 'RET'
	DC 'EXX'
	DC 'CPL'
	DC 'DAA'
	DC 'RLCA'
	DC 'RRCA'
	DC 'RLA'
	DC 'RRA'
	DC 'NOP'
	DC 'HALT'
	DC 'DI'
	DC 'EI'
	DC 'SCF'
	DC 'CCF'
	DC 'ORG'
	DC 'ENT'
	DC 'EQU'
	DC 'WORK'
	DC 'DB'
	DC 'DW'
	DC 'DM'
	DC 'DS'
	DC '!ASSM'
	DC '!CONT'
	DC 'LOADTEXT'
	DC 'LOADCODE'
	DC 'BC'
	DC 'DE'
	DC 'HL'
	DC 'IX'
	DC 'IY'
	DC 'SP'
	DC 'AF'
	DC '(C)'
	DC 'B'
	DC 'C'
	DC 'D'
	DC 'E'
	DC 'H'
	DC 'L'
	DC '(HL)'
	DC 'A'
	DC '(BC)'
	DC '(DE)'
	DC 'HX'
	DC 'LX'
	DC 'HY'
	DC 'LY'
	DC 'I'
	DC 'R'
	DC 'NZ'
	DC 'Z'
	DC 'NC'
	DC 'PO'
	DC 'PE'
	DC 'P'
	DC 'M'
	DC '!ON'
	DC '!OFF'
	DC '(SP)'
	DC 'AF\''

	ENDMODULE
